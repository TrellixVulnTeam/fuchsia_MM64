# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("$zx/kernel/params.gni")

group("pci") {
  deps = [
    ":bus-pci",
    ":bus-pci.proxy",
  ]
}

# The userspace PCI bus driver uses this set, as well as the
# automated tests.
source_set("user_pci") {
  sources = [
    "allocation.cpp",
    "bridge.cpp",
    "bus.cpp",
    "capabilities.cpp",
    "config.cpp",
    "device.cpp",
    "upstream_node.cpp",
  ]
  public_deps = [
    "$zx/system/banjo/ddk-protocol-pciroot",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/pretty",
    "$zx/system/ulib/region-alloc",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zxcpp",
  ]
}

if (enable_user_pci) {
  driver("bus-pci") {
    sources = [
      # The driver needs bind glue, unlike tests.
      "bind.cpp",
    ]
    deps = [
      ":user_pci",
    ]
  }

  driver("bus-pci.proxy") {
    sources = [
      "device_proxy.cpp",
    ]
    deps = [
      "$zx/system/banjo/ddk-protocol-pci",
      "$zx/system/ulib/ddk",
      "$zx/system/ulib/ddktl",
      "$zx/system/ulib/zxcpp",
    ]
  }
} else {
  driver("bus-pci") {
    sources = [
      "kpci/kpci.c",
    ]
    deps = [
      "$zx/system/banjo/ddk-protocol-pci",
      "$zx/system/banjo/ddk-protocol-pciroot",
      "$zx/system/banjo/ddk-protocol-platform-device",
      "$zx/system/banjo/ddk-protocol-sysmem",
      "$zx/system/ulib/ddk",
      "$zx/system/ulib/zircon",
    ]
  }

  driver("bus-pci.proxy") {
    sources = [
      "kpci/proxy.c",
    ]
    deps = [
      "$zx/system/banjo/ddk-protocol-pci",
      "$zx/system/banjo/ddk-protocol-pciroot",
      "$zx/system/banjo/ddk-protocol-platform-device",
      "$zx/system/banjo/ddk-protocol-sysmem",
      "$zx/system/ulib/ddk",
      "$zx/system/ulib/zircon",
      "$zx/system/ulib/zxcpp",
    ]
  }
}

test("pci_tests") {
  sources = [
    "test/allocation_tests.cpp",
    "test/config_tests.cpp",
    "test/device_tests.cpp",
    "test/main.cpp",
  ]
  deps = [
    ":user_pci",
    "$zx/system/dev/lib/fake_ddk",
    "$zx/system/ulib/hwreg",
    "$zx/system/ulib/zxtest",
  ]
}
